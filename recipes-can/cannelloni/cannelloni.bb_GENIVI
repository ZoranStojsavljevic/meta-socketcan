SUMMARY = "SocketCAN over Ethernet tunnel using UDP to transfer CAN frames between two machines"
SECTION = "socketcan"
LICENSE = "GPLv2"
LIC_FILES_CHKSUM = "file://gpl-2.0.txt;md5=b234ee4d69f5fce4486a80fdaf4a4263"

PR = "r0"

DEPENDS = "${@bb.utils.contains('DISTRO_FEATURES', 'systemd', 'systemd', '', d)}"

SRCREV = "${AUTOREV}"
SRC_URI = "git://github.com/mguentner/cannelloni \
           file://launch_cannelloni.sh \
           file://launch_cannelloni.service"

S = "${WORKDIR}/git"

inherit pkgconfig cmake
inherit systemd

SYSTEMD_SERVICE_${PN} = "launch_cannelloni.service"
INITSCRIPT_NAME = "launch_cannelloni.sh"

EXTRA_OECMAKE += "-DCMAKE_BUILD_TYPE=Release"

## cannelloni-dev-1.0 is registered as shlib provider for libcannelloni-common.so.0
SOLIBS = ".so"
FILES_SOLIBSDEV = ""

do_install() {
	install -d ${D}${libdir}
	install -m 0755 libcannelloni-common.so ${D}${libdir}

	install -d ${D}${bindir}
	install -m 0755 ${WORKDIR}/launch_cannelloni.sh ${D}${bindir}

	install -d ${D}${systemd_unitdir}/system
	install -m 0644 ${WORKDIR}/launch_cannelloni.service ${D}${systemd_unitdir}/system
}

RPROVIDES_${PN} += "${PN}-systemd"
RREPLACES_${PN} += "${PN}-systemd"
RCONFLICTS_${PN} += "${PN}-systemd"
